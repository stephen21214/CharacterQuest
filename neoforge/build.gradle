plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.78'
}

version = mod_version
group = mod_group_id

base {
    archivesName = 'characterquest-neo'
}

configurations {
    bstatsEmbed
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = neo_version

    runs {
        client {
            client()
            gameDirectory = project.file('run/client')
        }

        server {
            server()
            gameDirectory = project.file('run/server')
            programArgument '--nogui'
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version,
            neo_version: neo_version,
            neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id,
            mod_name: mod_name,
            mod_license: mod_license,
            mod_version: mod_version,
            mod_authors: mod_authors,
            mod_description: mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml', 'pack.mcmeta']) {
        expand replaceProperties
    }
}

dependencies {
    implementation("org.bstats:bstats-base:3.1.0")
    bstatsEmbed("org.bstats:bstats-base:3.1.0")
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.named('jar', Jar).configure {
    from {
        configurations.bstatsEmbed.collect { zipTree(it) }
    } {
        include 'org/bstats/**'
    }
}
